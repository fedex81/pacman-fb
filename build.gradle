plugins {
    id 'java'
}

def dateString = getDate()
version dateString + '-SNAPSHOT'
def archiveVersion = version
//version dateString
def jarNameHead = rootProject.name + "-" + version

group 'com.fbdev.helios.pacman'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jaudiolibs', name: 'audioservers-javasound', version: '2.0.0'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'

    //include local jars
    implementation fileTree(dir: 'lib', include: ['*.jar'])
}

def static getDate() {
    String date = new Date().format('yyMMdd')
    date.substring(0,2) + "." + date.substring(2)
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Helios-PacMan',
                'Implementation-Version': archiveVersion,
                'Main-Class': 'com.fbdev.SystemLoader',
                'Class-Path': configurations.runtimeClasspath.collect { "lib/" + it.getName() }.join(' ')
    }
    archivesBaseName = rootProject.name
    destinationDirectory = file("$buildDir/packageDir")
    doLast {
        println("Creating jar in folder: " + destinationDirectory)
    }
}


task packageDistribution(type: Zip) {
    dependsOn "jar", "copyRes", "writeToFile"
    archiveFileName = jarNameHead + ".zip"
    destinationDirectory = file("$buildDir/dist")

    from "$buildDir/packageDir"
    doLast {
        println("Creating package: " + archiveFileName +
                ", folder: " + destinationDirectory)
    }
}

task writeToFile {
    String jarName = jarNameHead + ".jar"
    List<String> javaAdvOpts =
            ["-XX:AutoBoxCacheMax=65536", "-XX:-DontCompileHugeMethods", "-XX:+AlwaysCompileLoopMethods"]
    //win: default to busy wait
    List<String> winOpts = ["-Dhelios.busy.wait=true"]
    List<String> sampleBase = [ "java",
                                "-Dlog4j.configurationFile=./log4j2.properties",
                                "-Djinput.enable=true -Djinput.native.location=lib",
                                "-jar", "$jarName"] as String[]
    sampleBase.addAll(1, javaAdvOpts)
    List<String> sampleUx = sampleBase.collect()
    sampleUx.add("\"\$@\"" + "\n")
    List<String> sampleWin = sampleBase.collect()
    sampleWin.addAll(1, winOpts)
    sampleWin.add("\"%1\"" + "\n")
    // create the folders if it does not exist.(otherwise it will throw exception)
    File extraFolder = new File( "${buildDir}/packageDir")
    if( !extraFolder.exists() ) {
        extraFolder.mkdirs()
    }
    // create the file and write text to it.
    new File("${buildDir}/packageDir/launcher.sh").text = sampleUx.join (" ")
    new File("${buildDir}/packageDir/launcher.bat").text = sampleWin.join (" ")
}

task copyRes {
    copy {
        from (".") {
            include "res/**"
        }
        into "$buildDir/packageDir"
        exclude "**/*.col", "**/site/**"
    }
    copy {
        from {
            ["CREDITS.md", "README.md", "HISTORY.md",
             "LICENSE.md", "helios.properties", "key.config"]
        }
        into "$buildDir/packageDir"
    }
    copy {
        from (".") {
            include "lib/**"
        }
        into "$buildDir/packageDir"
    }
    copy {
        from configurations.runtimeClasspath
        into "$buildDir/packageDir/lib"
    }
    copy {
        from ("res")
        include "*.properties"
        into "$buildDir/packageDir"
    }
}

test {
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

